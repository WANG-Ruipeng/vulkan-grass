#version 450
#extension GL_ARB_separate_shader_objects : enable
//#extension GL_EXT_debug_printf : enable

#define GRAVITY_MAGNITUDE	4.8
#define WIND_MAGNITUDE		1
#define WIND_FREQ			0.4

#define ORIENT_CULL			1
#define VIEW_FRUSTUM_CULL	1
#define DIST_CULL			1

#define ORIENTATION_THRESHOLD	0.6
#define FRUSTUM_TOLERANCE		-0.2	// exaggerated value to better see culling effect
#define MAX_DIST				20.0
#define NUM_DIST_LEVELS			10

#define WORKGROUP_SIZE 32
layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform CameraBufferObject {
    mat4 view;
    mat4 proj;
} camera;

layout(set = 1, binding = 0) uniform Time {
    float deltaTime;
    float totalTime;
};

struct Blade {
    vec4 v0;
    vec4 v1;
    vec4 v2;
    vec4 up;
};

// Add bindings to:
// 1. Store the input blades
layout(set = 2, binding = 0) buffer GrassBlades {
	Blade grassBlades[];
};

// 2. Write out the culled blades
layout(set = 2, binding = 1) buffer CulledBlades {
	Blade culledBlades[];
};

// 3. Write the total number of blades remaining
// The project is using vkCmdDrawIndirect to use a buffer as the arguments for a draw call
// This is sort of an advanced feature so we've showed you what this buffer should look like

layout(set = 2, binding = 2) buffer NumBlades {
   uint vertexCount;   // write the number of blades remaining here
   uint instanceCount; // = 1
   uint firstVertex;   // = 0
   uint firstInstance; // = 0
} numBlades;

layout(set = 2, binding = 3) uniform ObjectTransform {
    vec4 objectTrans;
} objTransform;

bool inBounds(float value, float bounds) {
    return (value >= -bounds) && (value <= bounds);
}

vec3 windMovement(vec3 v0, float time) {
	return WIND_MAGNITUDE * vec3(
			sin(WIND_FREQ * v0.x * time), 
			0.0, 
			cos(WIND_FREQ * v0.z * time));
}

bool inFrustum(vec3 p) {
    vec4 clipPos = camera.proj * camera.view * vec4(p, 1.0);
    float homogenousCoord = clipPos.w + FRUSTUM_TOLERANCE;
    return inBounds(clipPos.x, homogenousCoord) && 
		   inBounds(clipPos.y, homogenousCoord);
}

void main() {
	// Reset the number of blades to 0
	if (gl_GlobalInvocationID.x == 0) {
		numBlades.vertexCount = 0;
	}
	barrier(); // Wait till all threads reach this point

    // Apply forces on every blade and update the vertices in the buffer
	Blade currBlade = grassBlades[gl_GlobalInvocationID.x];
	vec3 v0 = currBlade.v0.xyz;
	vec3 v1 = currBlade.v1.xyz;
	vec3 v2 = currBlade.v2.xyz;
	vec3 up = currBlade.up.xyz;

	float orient = currBlade.v0.w;
	float height = currBlade.v1.w;
	float width = currBlade.v2.w;
	float stiffness = currBlade.up.w;

	/* 
	 * Gravity
	*/
	vec4 D = vec4(0.0, -1.0, 0.0, GRAVITY_MAGNITUDE);	// xyz = direction, w = magnitude of acceleration
	vec3 gE = normalize(D.xyz) * D.w; 					// environmental gravity
	vec3 t1 = normalize(vec3(-cos(orient), 0.0, sin(orient))); // dir along the blade width
	vec3 f = normalize(cross(t1, up));					// front facing dir of blade
	vec3 gF = 0.25 * length(gE) * f;					// front gravity

	vec3 totalGravity = gE + gF;

	/* 
	 * Recovery using Hooke's law
	 */
	vec3 iv2 = v0 + height * up; // original v2 before simulation
	vec3 recoveryForce = (iv2 - v2) * stiffness;

	/* 
	 * Wind
	 */
	vec3 wind = windMovement(v0, totalTime);
	float fd = 1 - abs(dot(normalize(wind), normalize(v2 - v0)));
	float fr = dot(v2 - v0, up) / height;
	vec3 windForce = wind * fd * fr; // fd*fr = wind alignment

	vec3 totalForce = (totalGravity + recoveryForce + windForce) * deltaTime;
	v2 += totalForce;

	/*
	 * Collision
	 */
	vec3 massCenter = (1.0 / 4.0) * v0 + (1.0 / 2.0) * v1 + (1.0 / 4.0) * v2;
	vec4 sphere1 = objTransform.objectTrans;
	vec3 sphereCenter = sphere1.xyz;
    float sphereRadius = sphere1.w;
	if (distance(v2, sphereCenter) < sphereRadius) {
		vec3 toSphereCenter = normalize(v2 - sphereCenter);
		vec3 pointOnSphereSurface = sphereCenter + toSphereCenter * sphereRadius;
		vec3 d = pointOnSphereSurface - v2;
		v2 += d;
    }
    else if (distance(massCenter, sphereCenter) < sphereRadius) {
		vec3 toSphereCenter = normalize(v2 - sphereCenter);
		vec3 pointOnSphereSurface = sphereCenter + toSphereCenter * sphereRadius;
		vec3 d = pointOnSphereSurface - v2;
		v2 += d * 4;
    }

	/*
	 * State validation
	 */
	// ensure v2 pos always above the ground
	
	v2 -= up * min(dot(up, v2 - v0), 0);

	// adjust v1 so blade is not stretched
	float lenProj = length(v2 - v0 - up * dot(v2-v0, up)); // length of the vector from v0 to v2 projected onto the ground plane
	v1 = v0 + height * up * max(1 - lenProj/height, 0.05 * max(lenProj/height, 1.0));

	// ensure blade length stays approximately the same
	float L0 = distance(v0, v2);
	float L1 = distance(v0, v1) + distance(v1, v2);
	float L = (2 * L0 + L1) / 3.0; // degree 2 Bezier curve
	float r = height / L;
	
	v1 = v0 + r * (v1 - v0); // corrected v1 & v2 pos
	v2 = v1 + r * (v2 - v1);

	currBlade.v1.xyz = v1;
	currBlade.v2.xyz = v2;
	grassBlades[gl_GlobalInvocationID.x] = currBlade;

	// Cull blades based on orientation, cam frustum and distance, and write them to the culled blades buffer
	// Note: to do this, you will need to use an atomic operation to read and update numBlades.vertexCount
	// You want to write the visible blades to the buffer without write conflicts between threads

	vec3 camPos = inverse(camera.view)[3].xyz;
	vec3 camToBlade = v0 - camPos;
	vec3 viewDir = camToBlade - up * dot(camToBlade, up);

// Cull blades that cannot be seen because of its orientation as our grass has no thickness
#if ORIENT_CULL
	if (abs(dot(normalize(viewDir), t1)) < ORIENTATION_THRESHOLD) {
		return;
	}
#endif

// Cull blades that are not in the camera frustum
#if VIEW_FRUSTUM_CULL
	vec3 m = 0.25 * v0 + 0.5 * v1 + 0.25 * v2; // approx midpoint of Bezier curve
	if (!inFrustum(v0) && !inFrustum(v2) && !inFrustum(m)) {
		return;
	}
#endif

// Cull blades that are too far away
#if DIST_CULL
	float dProj = length(viewDir); // projected dist of view vector onto the local plane defined by blade's up-vector
    int n = NUM_DIST_LEVELS;

	if (gl_GlobalInvocationID.x % n < int(floor(n * (1.0 - dProj / MAX_DIST)))) {
		return;
	}
#endif

	culledBlades[atomicAdd(numBlades.vertexCount, 1)] = grassBlades[gl_GlobalInvocationID.x];
}
